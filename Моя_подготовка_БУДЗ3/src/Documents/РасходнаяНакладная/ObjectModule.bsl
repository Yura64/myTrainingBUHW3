
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Управленческий.Записывать = Истина;
	Движения.Управленческий.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ПолеПространстваБлокировки = "Субконто"
		+ ПланыСчетов.Управленческий.Товары.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконто.Склад).НомерСтроки;
	ЭлементБлокировки.УстановитьЗначение(ПолеПространстваБлокировки, Склад);
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Свойство, "Свойство");
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ПолеПространстваБлокировки = "Субконто"
		+ ПланыСчетов.Управленческий.Материалы.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконто.Склад).НомерСтроки;
	ЭлементБлокировки.УстановитьЗначение(ПолеПространстваБлокировки, Склад);
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Материалы);
	
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК ДокументКоличество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК ДокументСумма
		|ПОМЕСТИТЬ ДанныеДокумента
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УправленческийОстатки.Счет КАК Счет,
		|	УправленческийОстатки.Субконто1 КАК Номенклатура,
		|	УправленческийОстатки.Субконто3 КАК Свойство,
		|	УправленческийОстатки.КоличествоОстатокДт КАК КоличествоОстаток
		|ПОМЕСТИТЬ ОстаткиПоСкладам
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(
		|			&МоментВремени,
		|			Счет = &СчетТовары,
		|			&СубконтоТовары,
		|			(Субконто1, Субконто2, Субконто3) В
		|				(ВЫБРАТЬ
		|					ДанныеДокумента.Номенклатура,
		|					&Склад,
		|					ДанныеДокумента.Свойство
		|				ИЗ
		|					ДанныеДокумента КАК ДанныеДокумента)) КАК УправленческийОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УправленческийОстатки.Счет,
		|	УправленческийОстатки.Субконто1,
		|	&Свойство,
		|	УправленческийОстатки.КоличествоОстатокДт
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(
		|			&МоментВремени,
		|			Счет = &СчетМатериалы,
		|			&СубконтоМатериалы,
		|			(Субконто1, Субконто2) В
		|				(ВЫБРАТЬ
		|					ДанныеДокумента.Номенклатура,
		|					&Склад
		|				ИЗ
		|					ДанныеДокумента КАК ДанныеДокумента)) КАК УправленческийОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УправленческийОстатки.Счет КАК Счет,
		|	УправленческийОстатки.Субконто1 КАК Номенклатура,
		|	УправленческийОстатки.Субконто3 КАК Свойство,
		|	УправленческийОстатки.КоличествоОстатокДт КАК КоличествоСебестоимость,
		|	УправленческийОстатки.СуммаОстатокДт КАК СуммаСебестоимость
		|ПОМЕСТИТЬ Себестоимость
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(
		|			&МоментВремени,
		|			Счет = &СчетТовары,
		|			&СубконтоТовары,
		|			(Субконто1, Субконто3) В
		|				(ВЫБРАТЬ
		|					ДанныеДокумента.Номенклатура,
		|					ДанныеДокумента.Свойство
		|				ИЗ
		|					ДанныеДокумента КАК ДанныеДокумента)) КАК УправленческийОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УправленческийОстатки.Счет,
		|	УправленческийОстатки.Субконто1,
		|	&Свойство,
		|	УправленческийОстатки.КоличествоОстатокДт,
		|	УправленческийОстатки.СуммаОстатокДт
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(
		|			&МоментВремени,
		|			Счет = &СчетМатериалы,
		|			&СубконтоМатериалы,
		|			(Субконто1, Субконто2) В
		|				(ВЫБРАТЬ
		|					ДанныеДокумента.Номенклатура,
		|					&Склад
		|				ИЗ
		|					ДанныеДокумента КАК ДанныеДокумента)) КАК УправленческийОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.Номенклатура,
		|	НЕ ДанныеДокумента.Номенклатура.Деталь КАК ЭтоТовар,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДанныеДокумента.Номенклатура) КАК НоменклатураПредставление,
		|	ДанныеДокумента.Свойство,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДанныеДокумента.Свойство) КАК СвойствоПредставление,
		|	ДанныеДокумента.ДокументКоличество,
		|	ДанныеДокумента.ДокументСумма,
		|	ЕСТЬNULL(ОстаткиПоСкладам.КоличествоОстаток, 0) КАК КоличествоПоСкладу,
		|	ЕСТЬNULL(Себестоимость.КоличествоСебестоимость, 0) КАК КоличествоСебестоимость,
		|	ЕСТЬNULL(Себестоимость.СуммаСебестоимость, 0) КАК СуммаСебестоимость
		|ИЗ
		|	ДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСкладам КАК ОстаткиПоСкладам
		|		ПО ДанныеДокумента.Номенклатура = ОстаткиПоСкладам.Номенклатура
		|			И ДанныеДокумента.Свойство = ОстаткиПоСкладам.Свойство
		|		ЛЕВОЕ СОЕДИНЕНИЕ Себестоимость КАК Себестоимость
		|		ПО ДанныеДокумента.Номенклатура = Себестоимость.Номенклатура
		|			И ДанныеДокумента.Свойство = Себестоимость.Свойство";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Свойство", Справочники.СвойстваНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	СубконтоТовары = Новый Массив;
	СубконтоТовары.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	СубконтоТовары.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склад);
	СубконтоТовары.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Свойство);
	
	СчетТовары = ПланыСчетов.Управленческий.Товары;
	
	Запрос.УстановитьПараметр("СубконтоТовары", СубконтоТовары);
	Запрос.УстановитьПараметр("СчетТовары", СчетТовары);
	
	СубконтоМатериалы = Новый Массив;
	СубконтоМатериалы.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	СубконтоМатериалы.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склад);
	
	СчетМатериалы = ПланыСчетов.Управленческий.Материалы;
	
	Запрос.УстановитьПараметр("СубконтоМатериалы", СубконтоМатериалы);
	Запрос.УстановитьПараметр("СчетМатериалы", СчетМатериалы);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТЗ = РезультатЗапроса.Выгрузить(); // Для отладки.
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	СуммаПродаж = 0;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДокументКоличество = ВыборкаДетальныеЗаписи.ДокументКоличество;
		КоличествоПоСкладу = ВыборкаДетальныеЗаписи.КоличествоПоСкладу;
		
		Если ДокументКоличество > КоличествоПоСкладу Тогда
			Отказ = Истина;
			
			Если ВыборкаДетальныеЗаписи.ЭтоТовар Тогда
				ШаблонСообщения = НСтр("ru = 'По номенклатуре ""%1"" со свойством ""%2"" превышено количество остатка. Остаток: %3'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения,
					ВыборкаДетальныеЗаписи.НоменклатураПредставление,
					ВыборкаДетальныеЗаписи.СвойствоПредставление,
					КоличествоПоСкладу);
			Иначе
				ШаблонСообщения = НСтр("ru = 'По номенклатуре ""%1"" превышено количество остатка. Остаток: %2'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ВыборкаДетальныеЗаписи.НоменклатураПредставление, КоличествоПоСкладу);
			КонецЕсли;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСебестоимость = ВыборкаДетальныеЗаписи.КоличествоСебестоимость; 
		СуммаСебестоимость = ВыборкаДетальныеЗаписи.СуммаСебестоимость;
		
		Движение = Движения.Управленческий.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.Период = Дата;
		Движение.КоличествоКт = ДокументКоличество;
		Движение.Сумма = ?(Движение.КоличествоКт = КоличествоСебестоимость,
			СуммаСебестоимость,
			СуммаСебестоимость / КоличествоСебестоимость * Движение.КоличествоКт);
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склад] = Склад;
		
		Если ВыборкаДетальныеЗаписи.ЭтоТовар Тогда
			Движение.СчетКт = СчетТовары;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Свойство] = ВыборкаДетальныеЗаписи.Свойство;
		Иначе
			Движение.СчетКт = СчетМатериалы;
		КонецЕсли;
		
		СуммаПродаж = СуммаПродаж + ВыборкаДетальныеЗаписи.ДокументСумма;
	КонецЦикла;
	
	Если Не Отказ Тогда
		// регистр Управленческий 
		Движение = Движения.Управленческий.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.Период = Дата;
		Движение.Сумма = СуммаПродаж;
	КонецЕсли;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
